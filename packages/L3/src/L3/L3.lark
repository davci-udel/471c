%import common.WS
%ignore WS

PROGRAM.2   : "l3"
LET.2       : "let"
LETREC.2    : "letrec"
REFERENCE.2 : "reference"
ABSTRACT.2  : "abstract" | LAMBDA
IMMEDIATE.2 : "immediate"
PRIMITIVE   : "+" | "-" | "*"
BRANCH.2    : "if"
ALLOCATE.2  : "allocate"
LOAD.2      : "load"
STORE.2     : "store"
BEGIN.2     : "begin"
LAMBDA.2    : "\\" | "lambda" | "Î»"
INTEGER     : ("0".."9")+ 
IDENTIFIER  : /[a-zA-Z_][a-zA-Z0-9_]*/
COMPARISON_OPERATOR    : "<" | "=="

parameters  : IDENTIFIER*
number      : INTEGER
nat         : INTEGER
binding     : IDENTIFIER term | "(" IDENTIFIER term ")"
bindings    : binding*

term        : let
            | letrec
            | reference
            | abstract
            | apply
            | immediate
            | primitive
            | branch
            | allocate
            | load
            | store
            | begin
            | number
            | IDENTIFIER

terms       : term*

program     : "(" PROGRAM "(" parameters ")" term ")"
let         : "(" LET "(" bindings ")" term ")"
letrec      : "(" LETREC "(" bindings ")" term ")"
reference   : "(" REFERENCE "(" IDENTIFIER ")" ")"
abstract    : "(" ABSTRACT "(" parameters ")" term ")"
apply       : "(" terms ")"
immediate   : "(" IMMEDIATE "(" number ")" ")"
primitive   : "(" PRIMITIVE term term ")"
branch      : "(" BRANCH "(" COMPARISON_OPERATOR term term ")" term term ")"
allocate    : "(" ALLOCATE nat ")"
load        : "(" LOAD term nat ")"
store       : "(" STORE term nat term ")"
begin       : "(" BEGIN terms ")"